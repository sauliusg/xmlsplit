#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author$
#$Date$ 
#$Revision$
#$URL$
#------------------------------------------------------------------------------
#*
#  Split concatenated XML file stream into separate XML files. Each
#  resulting file will be well-formed if the original files were
#  well-formed before concatenation.
#**

use strict;
use warnings;

BEGIN {
    use Cwd qw(abs_path);
    my $base_dir = abs_path(__FILE__);
    $base_dir =~ /(.*)\/[^\/]+$/;
    $base_dir = $1;
    $base_dir =~ s/\/(?:bin|scripts)$//;
    push(@INC, $base_dir . "/lib/perl5");
    push(@INC, $base_dir . "/externals/SOptions");
    push(@INC, $base_dir . "/externals/SUsage");
}

use File::Basename qw(basename);
use SOptions qw(getOptions);
use SUsage qw(usage);

my $xml_head = '<?xml ';

my $output_dir = '';
my $prefix = basename( $0 ) . '_';
my $suffix = '.xml';
my $digits = 4; # Number of decomal digits used to number the split files.

#* USAGE:
#*     $0 --options input1.xmlcat input*.xmlcat
#*     $0 --options < input.xmlcat
#* 
#* OPTIONS:
#*
#*    --help,--usage         Print a short usage message (this message) and exit.
#**

@ARGV = getOptions(
    "-d,--digits" => \$digits,
    "-o,--output-directory" => \$output_dir,
    "-p,--prefix" => \$prefix,
    "-s,--suffix" => \$suffix,
    "--options" => sub { 
        print "$0: The '--options' option is a placehoder.\n";
        print "$0: It should be replaced by one of the following options:\n";
        ## SUsage::usage;
        open( SCRIPT, $0 ) or die $!;
        while( <SCRIPT> ) {
            if( /^#\*\s+OPTIONS:/../^#\*\*/ ) {
                s/^#\*\s+OPTIONS://;
                s/^#\*\*?\s+//;
                print;
            }
        }
        close( SCRIPT );
        exit
    },
    "--help,--usage" => sub { usage; exit },
);

# The redefinition of '$/' must be done after the option processing,
# otherwise 'SUsage::usage', '--help' and '--options' options will not
# work as expected.

# Remove trailing '/' characters
$output_dir =~ s/\/*$//;
$output_dir = '.' unless $output_dir;

my $format = sprintf( "%%0%dd", $digits );

if( ! -e $output_dir ) {
    mkdir $output_dir;
}

local $/ = $xml_head;

my $n = 1;
while(<>) {
    s/\Q$xml_head\E$//;
    next if $_ eq "";
    my $filename = $output_dir . '/' .
        $prefix . sprintf( $format, $n ) . $suffix;

    open( my $fh, ">$filename" ) or
        die "could not open file '$filename' for writing: $!";
    print $fh $xml_head, $_;
    close($fh);

    $n++;
}
